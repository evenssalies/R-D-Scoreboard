/*

 R&D Scoreboard.
 
*/

cls
clear all
set maxvar 32000
set more off
global		HTTP="http://www.frequency.fr/"
local		DO="$HTTP"+"localhost.do"
do			"`DO'" doc

/************************************************************************
* Crée le fichier "scoreboard_panel.dta" (le Scoreboard original épuré) *
*************************************************************************/


*local 			FILEIN="Scoreboard_panel_2024.xlsx"
*import excel using 	"`FILEIN'", sheet("data") clear
*import excel using 	"Scoreboard_panel_2024.xlsx", clear
*save				"Scoreboard_panel_original.dta", replace 
use				"scoreboard_panel_original.dta", clear

/*	Variable SORT pour remettre le fichier dans l'ordre de départ si néc. */
generate		SORT=_n

/*	Vire la première ligne des noms des variables et créer mes propres noms */
drop in 		1

/* 	RAISON
		Deux noms de variables : 
			A (company) : le nom dans la base (la marque, etc.)
			AT (company_name) : le nom légal (sur les marchés, etc.),
		AT a beaucoup de missing, garder A */
drop			AT
rename			A RAISON

/*	YEAR 
		C'est l'année de réalisation des valeurs des autres variables. Par ex.,
		"The 2024 EU Industrial R&D Investment Scoreboard" porte sur les valeurs
		comptable de l'année 2023 */
rename			B YEAR
destring		YEAR, replace

/*		Années liées à l'entreprise
			AU (year_inc)
			AV (first_year_SB)
			AW (last_year_SB)
			AX (year_reporting) */
drop			AU-AX

/*	RANK
		K (world_rank)
		L (eu_rank). 
			Remarque :	beaucoup de missing pour K (58 %), L (85 %).
			Je calculerai les rangs moi-même. */
count if 		missing(K)
di				r(N)/_N
count if 		missing(L)
di				r(N)/_N
drop			K L							

/*	ICBNAMEx */
do				"scoreboard_panel_icb.do"
	
/*	COUNTRY, code iso-2, REGION */
do				"scoreboard_panel_country.do"
	
/*	RDINV 
		Remarque : il y a 3 variables de RD (R&D Scoreboard, 2023, p. 174) :
			S (rnd_23) :	R&D en euro courant (taux de 2023),
			T (rnd_euro) :	R&D en euro courant (taux de l'année),
			U (rnd_org) : 	R&D dans la monnaie courante du pays. 
			V et W :		en PPA, etc. */
drop			S U V W
rename			T RDINV
destring		RDINV, replace
 
/*	SALES
		Remarque : il y a 3 var. de Ch. d'A (R&D Scoreboard, 2023, p. 174) :
			X (netsales_23) :	Ch. d'A. net, euro courant (taux de 2023),
			Y (netsales_euro) :	Ch. d'A. net, euro courant (taux de l'ann.),
			Z (netsales_org) : 	Ch. d'A. net, monnaie courante du pays. */
drop			X Z AA AB
rename			Y SALES
order			SALES, after(RDINV)
destring		SALES, replace

/*	EMPL */
rename			AM EMPL
order			EMPL, after(SALES)
destring		EMPL, replace

/*	PROFITRATE (pour l'instant, on calcule PROFIT, pas PROFITRATE)
		Variable utile pour le b-index-loss à côté du b-index-profit
		Remarque : il y a 3 var. de profit (R&D Scoreboard, 2023, p. 174) :
			AH (profit_23) :		Ch. d'A. net, euro courant (taux de 2023),
			AI (profit_euro) :	Ch. d'A. net, euro courant (taux de l'ann.),
			AJ (profit_org) : 	Ch. d'A. net, monnaie courante du pays.

			Garder les profits négatifs pour choper le bindex (loss) */
drop			AH AJ AK AL
rename			AI PROFIT
destring		PROFIT, replace

/* MCAPI n'est pas disponible toutes les années (40 % de missing). On vire. */
drop			AN-AR
 
/*	CAPEX
		Remarque : il y a 3 var. d'inv (R&D Scoreboard, 2023, p. 174) :
				AC (capex_23) :		Inv., euro courant (taux de 2023),
				AD (capex_euro) :	Inv., euro courant (taux de l'ann.),
				AE (capex_org) : 	Inv., monnaie courante du pays. */
drop			AC AE AF AG
rename			AD CAPEX
destring		CAPEX, replace

/*	EXCHR	(how many currency in 1 euro)
	CURRE	(currency)
	R		GDP Deflator
	Garde ex_rate_euro et currency pour le comparer à ma table countries.dta */
drop			N P Q R
rename			O EXCHR
destring		EXCHR, replace
rename			M CURRE

/*	Global ultimate owner */
drop			AS

/*	Arrange les variables */
order			RAISON YEAR ICBNAME* NACEREV2 COUNTRY REGION ///
				RDINV SALES EMPL CAPEX alpha2 PROFIT
compress
sort			RAISON YEAR

save			"scoreboard_panel.dta", replace

/*	Importe les secteurs : NAFA88, NACEREV2 */
do				"scoreboard_panel_a88_nacerev2.do"

sort			RAISON YEAR
order			alpha2, last
save			"scoreboard_panel.dta", replace

/***********************************
*	Classes de tailles d'effectifs *
************************************/

use				"scoreboard_panel.dta", replace
do				"scoreboard_panel_empl.do"
tabulate		TAILLE, generate(TAILLE_)
drop			if TAILLE==.

/******************************
* Passage au niveau sectoriel *
*******************************/

*use "scoreboard_panel.dta", replace
keep if 		inrange(YEAR, 2003, 2023)
generate		nb_firmes = 1
collapse 		(sum) RDINV SALES CAPEX PROFIT EMPL TAILLE_* ///  sommes
				(count) nb_firmes, ///  nombre d'entreprises
				by(COUNTRY YEAR NACEREV2_42)

rename 			nb_firmes N_FIRMES
merge m:1 		NACEREV2_42 using "euklems_nacerev2.dta", ///
				keep(match) nogen keepusing(NACEREV2_42_NAME_FR)
save			"scoreboard_panel.dta", replace

/*************
* Deflateurs * ATTENTION, on perd 54-22 = 32 pays au passage !!!
**************/

do				"scoreboard_panel_deflator.do"		
compress

/**********
* B-index * ATTENTION, on perd 1 pays (Pérou)
***********/

do				"scoreboard_panel_rd_b-index.do"
compress

/*	Ajouter un commentaire ici !!! */

generate		BINDEX=(BI_P_PME*(TAILLE_1+TAILLE_2)+ ///
				BI_P_GE*(TAILLE_3+TAILLE_4))/N_FIRMES if PROFIT>0
replace			BINDEX=(BI_L_PME*(TAILLE_1+TAILLE_2)+ ///
				BI_L_GE*(TAILLE_3+TAILLE_4))/N_FIRMES if PROFIT<=0
drop			BI_*
				
/***************************************
* Construction des variables de stocks *
****************************************/

* Importe le stock de capital physique */
*do ...

/*	Importe le stock de capital R&D */
*do ...

/*******************************
* Ratios et indicateurs utiles *
********************************/

* Intensité de R&D
generate		RDINV_SALES = RDINV/SALES

* Intensité CAPEX
generate		CAPEX_SALES = CAPEX/SALES

* R&D par employé (milliers d'euros de R&D/employé)
gen   			RDINV_EMPL	= 1000*RDINV/EMPL
save 			"scoreboard_sector_panel.dta", replace

/***************************
* Etiquetter des variables *
****************************/

label variable	BINDEX "Prix implicite de la R&D"

label variable	CAPEX "FBCF (M euros)"
*label variable	KSTOCK "Stock de capital physique (tx dépré. 8 %)"
*label variable	RSTOCK "Stock de capital de connaissance (tx dépré. 8 %)"

label variable	CAPEX_SALES "FBCF/SALES"

label variable	COUNTRY "Nom du pays (EN)"
*order			COUNTRY, after(REGION)

label variable	EMPL "Effectif total"

label variable	NACEREV2_42 "Secteur (code, 88 divis. NACE Rev 2 regroupées)"
label variable	NACEREV2_42_NAME_FR "Secteur (nom)"

label variable	N_FIRMES "Nb. d'entreprises dans le secteur"

label variable	PROFIT "Profit operationel (M euros)"
*label variable	PROFITRATE "Profit operationel/Ch. d'a. net (%)"

label variable	RDINV "Dépenses de R&D (M euros)"
label variable	RDINV_EMPL "Intensité de R&D (k euros/employé)"
label variable	RDINV_SALES "Intensité de R&D (R&D/CA)"

label variable	SALES "Chiffre d'affaires net (M euros)"

label variable	TAILLE_1 "TPE" 
label variable	TAILLE_2 "PME" 
label variable	TAILLE_3 "ETI" 
label variable	TAILLE_4 "GE" 

label variable	YEAR "Année"

label variable	alpha2 "Code iso 2 du pays"

*label variable	RAISON "Raison sociale (tête de groupe)"
*label variable	P "Prix de la production (2015 = 100)"
*label variable	PM "Prix des biens intermédiaires (2015 = 100)"
*label variable	PK "Prix de la FBCF (2015 = 100)"
*label variable	PR "Prix de la R&D (2015 = 100)"

encode			COUNTRY, generate(COUNTRYCODE)
encode			NACEREV2_42, generate(NACEREV2_42CODE)
compress
save			"scoreboard_panel.dta", replace
		
/**************************************************
* Statistiques descriptives, tableaux, graphiques *
***************************************************/

/*	Graphique des centiles de R&D, afin de voir que la part des entreprises
		ayant un niveau de R&D élevé est bien plus grands aux US. Le niveau
		de R&D minimum dans l'échantillon est bien plus bas en Europe, car
		en fixant le #entreprises, et sachant que plus de GE aux US */
local			TEMP1=_N
drop if 		EMPL==.|RDINV==.
local			TEMP2=`TEMP1'-_N
di				"`TEMP1' `TEMP2' " _N

su				EMPL, d
tabulate		YEAR
di r(r)

keep 			if YEAR==2022
keep 			RAISON COUNTRY REGION RDINV EMPL
save			"temp.dta", replace

tabulate 		COUNTRY
di r(r)

sort 			RDINV
quietly: su 	RDINV
gen 			I=recode(RDINV,r(min),3,30,300,3000,r(max))
tabulate 		I
su 				EMPL if I>=30, d
sort 			EMPL
quietly: su 	EMPL
gen 			J=recode(EMPL,9,249,4999,r(max))
tabulate 		J
su				EMPL, d

/* Fonction de répartition cumulée */
use				"temp.dta", clear
table			REGION
foreach 		R in "China" "EU" "Japan" "ROW" "US" {
	use			"temp.dta", clear
	keep if		REGION=="`R'"
	/* drop countries that have less than 3 firms */
	egen		N=count(RAISON), by(COUNTRY)
	drop if		N<3
	sort		RDINV
	gen			CE=100*_n/_N
	local		FILE="REGION_`R'"
	save		`FILE', replace
}
				
use					"REGION_China.dta", clear				
foreach 			R in "EU" "Japan" "ROW" "US" {
	local			FILE="REGION_`R'"
	append using	`FILE'
}
gen				RDINVl=log(RDINV)
label variable	RDINVl "R&D (M €)"
label variable	CE "Centile"
graph twoway 	(line RDINVl CE if REGION=="China", lcolor(yellow) ///
					ylabel( 0.405     "1,5" 1.098   "3" 3.401   "30" ///
						    3.912    "50"   5.703 "300" 8.006 "3000" ///
						   10.308 "30000") ///
					xlabel(0(10)100) ///
					legend(row(1) position(6) ///
						label(1 "Chine") ///
						label(2 "UE") ///
						label(3 "Japon") ///
						label(4 "RdM") ///
						label(5 "EU"))) ///
				(line RDINVl CE if REGION=="EU",  	lcolor(black)) ///
				(line RDINVl CE if REGION=="Japan", lcolor(red)) ///
				(line RDINVl CE if REGION=="ROW", 	lcolor(brown)) ///
				(line RDINVl CE if REGION=="US", 	lcolor(blue))
tabulate 		COUNTRY, sort
di				r(r)


/* 		Combien d'entreprises differentes depuis 2000 : 6225 */
keep			RAISON
duplicates drop
display			_N 

/* 		Combien d'entreprises differentes en France depuis 2000 : 230 */
use				"scoreboard_panel.dta", clear
keep if 		COUNTRY=="France"&YEAR>=2000
keep			RAISON
duplicates drop
display			_N 

/*		Combien de missing par année */
generate		TEMP1=(RDINV==.)
egen			TEMP2=sum(TEMP), by(YEAR)
egen			TEMP3=count(RAISON), by(YEAR)
replace			TEMP3=100*TEMP2/TEMP3
tabstat			TEMP3, by(YEAR)
drop			TEMP*
/*		Remarque : à partir de 2019, il y a moins de 5 % de missing,
					1,66 % en 2020, 0,5 % en 2021 et 0,18 % en 2022 .*/

/*	Combien d'années par entreprise.
		   27520/20 = 1376 entreprises 20 années
			2983/19 =  157 19		 
			5958/18 =  331 18
			4267/17 =  251 17
			5008/16 =  313 16
			4845/15 =  323 15
			5432/14 =  388 14
			5226/13 =  402 13
			7908/12 =  659 12
			7854/11 =  714 11
			8800/10 =  880 10
			2412/9  =  268  9
			   1/1  =    1  entreprise 1 année (la Corée du Sud) */
egen			ANNEEBYRAISON=count(ANNEE), by(RAISON)
tabulate		ANNEEBYRAISON  /*	Freq./N entreprises observees N années */
drop			ANNEEBYRAISON

/*	Combien d'entreprises par année */
tabulate		ANNEE, sort

histogram ANNEEBYRAISON, ///
    title("Entreprises par Année") ///
    xtitle("Nombre d'année") ///
    ytitle("Nombre d'entreprise") ///
    color(%50) ///
    lcolor(black) ///
    lwidth(medium) ///
    start(0) width(1) ///
    scheme(s2color)

/*		Remarque : sur 20 années,
			2013 est celle ayant le plus d'entreprises
			2019 (2020) est en 12e (14e) position */

/*		Distribution de la taille (effectifs salariés)
			Pour ça faut être sûr que l'unité est constante dans le temps. */
by 				ANNEE, sort: su EMPL, d

/* 		La plus petite ent. française en nb. d'employé.es est-elle une PME ?
			Abionyx, dans la Santé, 4 salariés, environ 4 millions de RD.
			Inspection visuelle */
sort			EMPL

/*	Nombre d'entreprises en France et dans le reste de l'Europe par année */
sort			ANNEE PAYSNOM
egen 			TEMP=count(RAISON), by(ANNEE PAYSNOM)
table			ANNEE if PAYSNOM=="France"
table			ANNEE if PAYSNOM!="France"
drop			TEMP

/*		Répartition de la RD par secteur et par taille d'entreprise
drop if			RDINV==.
egen			TEMP=sum(RDINV) if PAYS=="France"&ANNEE==2019, by(A88)
keep			A88 TEMP
duplicates drop
sort			A88
drop if			TEMP==.
egen			TOTAL=sum(TEMP)
replace			TEMP=100*TEMP/TOTAL

drop if			RDINV==.
keep if 		PAYS=="France"&ANNEE==2019
egen			TEMP=sum(RDINV), by(TAILLE)
keep			TAILLE TEMP
duplicates drop
egen			TOTAL=sum(TEMP)
replace			TEMP=100*TEMP/TOTAL */

/********************
* Synthetic control *
*********************/


/* Automobile */
use				"scoreboard_panel.dta", clear
keep if			NACEREV2_42CODE==14

/*		Pas de R&D */
drop if			alpha2=="BE"|alpha2=="SI"

/*		R&D nulle */
replace			RDINV=. if alpha2=="FI"&YEAR==2004
ipolate			RDINV SALES if alpha2=="FI", epolate generate(TEMP) 
replace			RDINV=TEMP if alpha2=="FI"&YEAR==2004
drop TEMP

/*		Trous dans la R&D */

ipolate			RDINV YEAR if alpha2=="IT", epolate generate(TEMP) 
replace			RDINV=TEMP if alpha2=="IT"&RDINV==.
drop TEMP

/*		Trous dans SALES */
drop if			alpha2=="LU"|alpha2=="IE"
ipolate			SALES YEAR if alpha2=="AT", epolate generate(TEMP) 
replace			SALES=TEMP if alpha2=="AT"&SALES==.
drop TEMP

ipolate			SALES YEAR if alpha2=="FR", epolate generate(TEMP) 
replace			SALES=TEMP if alpha2=="FR"&SALES==.
drop TEMP

ipolate			SALES YEAR if alpha2=="IT", epolate generate(TEMP) 
replace			SALES=TEMP if alpha2=="IT"&SALES==.
drop TEMP

ipolate			SALES YEAR if alpha2=="ES", epolate generate(TEMP) 
replace			SALES=TEMP if alpha2=="ES"&SALES==.
drop TEMP

/*		Démarrer l'année du dernier pays qui rentre dans la base

			Pour l'automobile, la Finlande rentre en 2004,
			donc virer l'année 2003 */

/* 		Déclarer le panel */
drop if			YEAR==2003
xtset			COUNTRYCODE YEAR

/*		Apparier les pays dans ce secteur */
xtline			BINDEX
drop if			alpha2=="NL"

xtset			COUNTRYCODE YEAR
synth			RDINV ///
				SALES EMPL PROFIT ///
				SALES(2007), ///
				trunit(6) trperiod(2008) xperiod(2004(1)2007) ///
				nested ///
				keep(scoreboard_panel_automobile) replace ///
				fig
				*unitnames(state_name)
				*counit(1 2 4) maxiter(20)


				
				
				
				
				
				
				
				
				
				
				
				
				
				
				
				
				
				
				